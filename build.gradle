// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.4.31'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.1"
        classpath "com.alibaba:arouter-register:1.0.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url "https://oss.jfrog.org/libs-snapshot" }
        maven{
            url 'file:'+ rootDir.getPath()+'/lib'
        }
    }
}

subprojects {
    def configureLint = {
        def baseLineFilePath = "$projectDir/lint-baseline.xml"

        // set lint options for all android modules
        android {
            lintOptions {
                baseline file(baseLineFilePath)
                checkDependencies false
                checkReleaseBuilds true
                abortOnError true
            }
        }

        // Add a task to delete the baseline file
        tasks.register("cleanLintBaseline", Delete) {
            delete file("$projectDir/lint-baseline.xml")
        }
    }

    afterEvaluate {
        // apply lint configuration to all android app modules
        plugins.withType(com.android.build.gradle.AppPlugin, configureLint)
        // apply lint configuration to all android library modules
        plugins.withType(com.android.build.gradle.LibraryPlugin, configureLint)
    }
}



task clean(type: Delete) {
    delete rootProject.buildDir
}